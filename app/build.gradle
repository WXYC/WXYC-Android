plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

// Load secrets from properties file
def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretsProperties = new Properties()
if (secretsPropertiesFile.exists()) {
    secretsProperties.load(new FileInputStream(secretsPropertiesFile))
}

android {
    namespace 'org.wxyc.wxycapp'
    compileSdk 36

    defaultConfig {
        applicationId "org.wxyc.WXYCCH"
        minSdk 24
        targetSdk 36
        versionCode 37
        versionName "1.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        // Add BuildConfig fields for secrets
        buildConfigField "String", "RAILWAY_ENDPOINT_URL", "\"${secretsProperties.getProperty("RAILWAY_ENDPOINT_URL", "")}\""
        buildConfigField "String", "SLACK_WEBHOOK_BASE_URL", "\"${secretsProperties.getProperty("SLACK_WEBHOOK_BASE_URL", "")}\""
        buildConfigField "String", "DISCOGS_API_KEY", "\"${secretsProperties.getProperty("DISCOGS_API_KEY", "")}\""
        buildConfigField "String", "DISCOGS_SECRET_KEY", "\"${secretsProperties.getProperty("DISCOGS_SECRET_KEY", "")}\""
        buildConfigField "String", "LASTFM_API_KEY", "\"${secretsProperties.getProperty("LASTFM_API_KEY", "")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig true
        compose true
    }
    
    lint {
        checkReleaseBuilds false
        abortOnError false
        warningsAsErrors false
        disable 'MissingTranslation', 'NotificationPermission'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.github.kittinunf.fuel:fuel:2.3.1'  // used to fetch json
    implementation 'com.google.code.gson:gson:2.10.1' // used to parse json into objects
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'


    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation "com.squareup.okhttp3:okhttp:4.9.2"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // Coroutine Lifecycle Scopes
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"

    // Compose BOM
    def composeBom = platform('androidx.compose:compose-bom:2024.12.01')
    implementation composeBom

    // Core Compose
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Activity Compose
    implementation 'androidx.activity:activity-compose:1.9.3'

    // ViewModel Compose
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7'

    // Coil for image loading
    implementation 'io.coil-kt:coil-compose:2.7.0'

    // Hilt
    implementation "com.google.dagger:hilt-android:2.48"
    kapt "com.google.dagger:hilt-compiler:2.48"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
}
